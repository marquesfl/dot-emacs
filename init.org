#+TITLE: Emacs Configuration File
#+BABEL: cache yes
#+PROPERTY: header-args :tangle yes
* Frist Compilation
This code replace itself with the actual configuration at first run 
#+begin_src emacs-lisp :tangle no
(require 'org) ; Use Org package
(find-file (concat user-emacs-directory "init.org")) ; Put init.org as initialization 
(org-babel-tangle) ; Extract just emacs-lisp source
(load-file (concat user-emacs-directory "init.el")) ; Insert the emacs-lisp from init.org to init.el
(byte-compile-file (concat user-emacs-directory "init.el")) ; Compile init.el for initialization.
#+end_src
* Lexical Scope
#+begin_src emacs-lisp
;;; -*- lexical-binding: t -*-
(set-language-environment "UTF-8")
#+end_src
* Package Config
Package managers for emacs
#+begin_src emacs-lisp
(require 'package)

(setq package-archives '(("melpa" . "https://melpa.org/packages/")
                         ("org" . "https://orgmode.org/elpa/")
                         ("elpa" . "https://elpa.gnu.org/packages/")))

(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))
#+end_src
* Installing Packages
#+begin_src emacs-lisp
(let* ((package--builtins nil)
       (packages
        '(auto-compile         ; automatically compile Emacs Lisp libraries
          cider                ; Clojure Interactive Development Environment
          clj-refactor         ; Commands for refactoring Clojure code
          company              ; Modular text completion framework
          counsel              ; Various completion functions using Ivy
          counsel-projectile   ; Ivy integration for Projectile
          haskell-mode         ; A Haskell editing mode
          jedi                 ; Python auto-completion for Emacs
          js2-mode             ; Improved JavaScript editing mode
          lsp-mode             ; LSP mode
          lsp-java             ; Java support for lsp-mode
          magit                ; control Git from Emacs
          markdown-mode        ; Emacs Major mode for Markdown-formatted files
          org                  ; Outline-based notes management and organizer
          org-bullets          ; Show bullets in org-mode as UTF-8 characters
          paredit              ; minor mode for editing parentheses
          projectile           ; Manage and navigate projects in Emacs easily
          slime)))             ; Superior Lisp Interaction Mode for Emacs
  (when (memq window-system '(mac ns))
    (push 'exec-path-from-shell packages)
    (push 'reveal-in-osx-finder packages))
  (let ((packages (seq-remove 'package-installed-p packages)))
    (print packages)
    (when packages
      ;; Install uninstalled packages
      (package-refresh-contents)
      (mapc 'package-install packages))))
#+end_src
* Local Config
** Linking init.org with init.el
#+begin_src emacs-lisp
(defun tangle-init ()
  "If the current buffer is 'init.org' the code-blocks are tangled, and the tangled file is compiled."
  (when (equal (buffer-file-name)
               (expand-file-name (concat user-emacs-directory "init.org")))
    ;; Avoid running hooks when tangling.
    (let ((prog-mode-hook nil))
      (org-babel-tangle)
      (byte-compile-file (concat user-emacs-directory "init.el")))))
(add-hook 'after-save-hook 'tangle-init)
#+end_src
** Yes or No
#+begin_src emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
#+end_src
** Backup Files
#+begin_src emacs-lisp
(defvar emacs-autosave-directory
  (concat user-emacs-directory "autosaves/")
  "This variable dictates where to put auto saves. It is set to a directory called autosaves located wherever your .emacs.d/ is located.")

;; Sets all files to be backed up and auto saved in a single directory.
(setq backup-directory-alist
      `((".*" . ,emacs-autosave-directory))
      auto-save-file-name-transforms
      `((".*" ,emacs-autosave-directory t)))
#+end_src
** Startup Time
#+begin_src emacs-lisp
(defun efs/display-startup-time ()
  (message "Emacs loaded in %s with %d garbage collections."
           (format "%.2f seconds"
                   (float-time
                     (time-subtract after-init-time before-init-time)))
           gcs-done))

(add-hook 'emacs-startup-hook #'efs/display-startup-time)
#+end_src

** Basic UI Config
#+begin_src emacs-lisp
(dolist (mode '(tool-bar-mode                ; No toolbars, more room for text
                scroll-bar-mode              ; No scroll bars either
                blink-cursor-mode))          ; The blinking cursor gets old
  (funcall mode 0))

;; Set up the visible bell
(setq visible-bell t)

;; Set fullscreen
(add-to-list 'default-frame-alist '(fullscreen . maximized))

;; Set default
(setq-default indent-tabs-mode nil)
(set-face-attribute 'default nil :height 160)
#+end_src
** Basic Modes Config
#+begin_src emacs-lisp
(dolist (mode
         '(abbrev-mode                  ; E.g. sopl -> System.out.println
           column-number-mode           ; Show column number in mode line
           delete-selection-mode        ; Replace selected text
           dirtrack-mode                ; directory tracking in *shell*
           global-company-mode          ; Auto-completion everywhere
           counsel-projectile-mode      ; Manage and navigate projects
           recentf-mode                 ; Recently opened files
           show-paren-mode))            ; Highlight matching parentheses
  (funcall mode 1))

(global-display-line-numbers-mode t)

;; Disable line numbers for some modes
(dolist (mode '(org-mode-hook
                term-mode-hook
                shell-mode-hook
                treemacs-mode-hook
                eshell-mode-hook))
  (add-hook mode (lambda () (display-line-numbers-mode 0))))
#+end_src
** Ivy
#+begin_src emacs-lisp
(setq ivy-wrap t
      ivy-height 25
      ivy-use-virtual-buffers t
      ivy-count-format "(%d/%d) "
      ivy-on-del-error-function 'ignore)
(ivy-mode 1)
#+end_src
** Complate anything
Company configuration
#+begin_src emacs-lisp
(setq company-idle-delay 0
      company-echo-delay 0
      company-dabbrev-downcase nil
      company-minimum-prefix-length 2
      company-selection-wrap-around t
      company-transformers '(company-sort-by-occurrence
                             company-sort-by-backend-importance))
#+end_src
* IDE Config
** LSP
#+begin_src emacs-lisp
(with-eval-after-load 'lsp-mode
  (define-key lsp-mode-map (kbd "C-c f") lsp-command-map)
  (add-hook 'lsp-mode-hook #'lsp-enable-which-key-integration))
#+end_src
** Lisp
#+begin_src emacs-lisp
(dolist (mode '(cider-repl-mode
                clojure-mode
                slime-repl-mode
                lisp-mode
                emacs-lisp-mode
                lisp-interaction-mode))
  ;; add paredit-mode to all mode-hooks
  (add-hook (intern (concat (symbol-name mode) "-hook")) 'paredit-mode))
#+end_src
** Slime
#+begin_src emacs-lisp
(setq inferior-lisp-program "sbcl")
#+end_src
** Python
#+begin_src emacs-lisp
(setq python-shell-interpreter "python3")
(add-hook 'python-mode-hook
          (lambda () (setq forward-sexp-function nil)))
#+end_src
** C/C++
#+begin_src emacs-lisp
(defun c-setup ()
  (local-set-key (kbd "C-c C-c") 'compile))

(add-hook 'c-mode-hook 'c-setup)
#+end_src
** Haskell
#+begin_src emacs-lisp
(add-hook 'haskell-mode-hook 'interactive-haskell-mode)
(add-hook 'haskell-mode-hook 'turn-on-haskell-doc-mode)
(add-hook 'haskell-mode-hook 'turn-on-haskell-indent)

(setq haskell-process-args-ghci
      '("-ferror-spans" "-fshow-loaded-modules"))

(setq haskell-process-args-cabal-repl
      '("--ghc-options=-ferror-spans -fshow-loaded-modules"))

(setq haskell-process-args-stack-ghci
      '("--ghci-options=-ferror-spans -fshow-loaded-modules"
        "--no-build" "--no-load"))

(setq haskell-process-args-cabal-new-repl
      '("--ghc-options=-ferror-spans -fshow-loaded-modules"))
#+end_src
* Keybindings
For custom bindigns add
#+begin_src emacs-lisp
(defvar custom-bindings-map (make-keymap)
  "A keymap for custom bindings.")
#+end_src
** Multiple Cursor
#+begin_src emacs-lisp
(define-key custom-bindings-map (kbd "C-c e")  'mc/edit-lines)
(define-key custom-bindings-map (kbd "C-c a")  'mc/mark-all-like-this)
(define-key custom-bindings-map (kbd "C-c n")  'mc/mark-next-like-this)
#+end_src
** Magit
#+begin_src emacs-lisp
(define-key custom-bindings-map (kbd "C-c m") 'magit-status)
#+end_src
** Counsel
#+begin_src emacs-lisp
(global-set-key (kbd "C-c i")   'swiper-isearch)
(global-set-key (kbd "M-x")     'counsel-M-x)
(global-set-key (kbd "C-x C-f") 'counsel-find-file)
(global-set-key (kbd "M-y")     'counsel-yank-pop)
(global-set-key (kbd "C-x b")   'ivy-switch-buffer)
#+end_src
** Company
#+begin_src emacs-lisp
(define-key company-active-map (kbd "C-d")   'company-show-doc-buffer)
(define-key company-active-map (kbd "C-n")   'company-select-next)
(define-key company-active-map (kbd "C-p")   'company-select-previous)
(define-key company-active-map (kbd "<tab>") 'company-complete)
#+end_src
** Projectile
#+begin_src emacs-lisp
(define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
#+end_src
** Normal Keys
#+begin_src emacs-lisp
(global-set-key (kbd "C-ç c") 'copy-region-as-kill)
(global-set-key (kbd "C-ç v") 'yank)
(global-set-key (kbd "C-ç x") 'kill-region)
(global-set-key (kbd "C-ç p") 'print-region)
(global-set-key (kbd "C-ç <tab>") 'switch-to-next-buffer)
#+end_src
** Activate the map
#+begin_src emacs-lisp
(define-minor-mode custom-bindings-mode
  "A mode that activates custom-bindings."
  t nil custom-bindings-map)
#+end_src
** Org
#+begin_src emacs-lisp
(global-set-key (kbd "C-ç a") 'org-agenda)
#+end_src
* Bibliography
[[https://github.com/larstvei/dot-emacs/blob/master/init.org][larstvei]]
[[https://github.com/daviwil/emacs-from-scratch/blob/master/Emacs.org][daviwil]]
